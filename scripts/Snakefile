import os
import snakemake_tools as tools

rule all:
    input:
        summary = "{}/../results.json".format(tools.SCRIPTS_DIR),
        website = "{}/website.complete.temp".format(tools.TEMPDIR),

rule website:
    input: tools.datasets
    output: temp("{}/website.complete.temp".format(tools.TEMPDIR))
    shell: "touch {output}"

rule docker:
    input: tools.push_images

rule docker_build:
    input: tools.build_images

rule summary:
    input:
        script = "collate_all.py",
        methods = tools.all_methods,
    params:
        dir = tools.TEMPDIR
    output: "{}/../results.json".format(tools.SCRIPTS_DIR)
    shell: "python3 {input.script} {params.dir} {output}"

rule collate_dataset:
    input:
        script = "collate_dataset.py",
        methods = tools.methods,
    params:
        dir = tools.TEMPDIR
    output: "{}/{{task}}/{{dataset}}.json".format(tools.RESULTS_DIR)
    shell:
        """python3 {input.script} {wildcards.task} {wildcards.dataset} \
        {params.dir}/{wildcards.task}/{wildcards.dataset} {output}"""

rule collate_method:
    input:
        script = "collate_method.py",
        meta = "{tempdir}/{task}/{dataset}/{method}.meta.json",
        metrics = tools.metrics,
    output: temp("{tempdir}/{task}/{dataset}/{method}.result.json")
    shell:
        """python3 {input.script} {wildcards.task} {input.meta} \
        {wildcards.tempdir}/{wildcards.task}/{wildcards.dataset}/{wildcards.method} \
        {output}"""

rule evaluate_metric:
    input:
        script = "evaluate_metric.py",
        data = "{tempdir}/{task}/{dataset}/{method}.method.h5ad",
        docker = tools.docker_push,
    output: temp("{tempdir}/{task}/{dataset}/{method}/{metric}.metric.json")
    params:
        workdir = tools.DOCKER_DIR,
        docker = tools.docker_command
    threads: tools.N_THREADS
    shell:
        """{params.docker} {params.workdir} {input.script} {wildcards.task} \
        {wildcards.metric} {input.data} {output} && \
        docker stop $CONTAINER'"""

rule run_method:
    input:
        script = "run_method.py",
        data = "{tempdir}/{task}/{dataset}.data.h5ad",
        docker = tools.docker_push,
    output:
        data = temp("{tempdir}/{task}/{dataset}/{method}.method.h5ad"),
        json = temp("{tempdir}/{task}/{dataset}/{method}.meta.json"),
    params:
        workdir = tools.DOCKER_DIR,
        docker = tools.docker_command
    threads: tools.N_THREADS
    shell:
        """{params.docker} {params.workdir} {input.script} {wildcards.task} \
        {wildcards.method} {input.data} {output.data} {output.json} && \
        docker stop $CONTAINER'"""

rule load_dataset:
    input:
        script = "load_dataset.py",
        code = "{}/../openproblems/version.py".format(tools.SCRIPTS_DIR),
        docker = tools.docker_push,
    output: temp("{tempdir}/{task}/{dataset}.data.h5ad")
    params:
        workdir = tools.DOCKER_DIR,
        docker = tools.docker_command
    threads: tools.N_THREADS
    shell: """{params.docker} {params.workdir} {input.script} {wildcards.task} \
           {wildcards.dataset} {output} && \
           docker stop $CONTAINER'"""

rule build_docker:
    input:
        dockerfile = "../docker/{image}/Dockerfile",
        requirements = tools.docker_requirements,
    output:
        "../docker/{image}/.docker_build"
    params:
        sourcedir = os.path.dirname(tools.SCRIPTS_DIR),
        user = "singlecellopenproblems"
    shell:
        """docker build -f {input.dockerfile} -t {params.user}/{wildcards.image} .. \
        && touch {output}"""

rule password_docker:
    output:
        filename = temp(".docker_password")
    run:
        with open(output.filename, 'w') as handle:
            handle.write(tools.DOCKER_PASSWORD)

rule login_docker:
    input:
        ".docker_password"
    output:
        temp(".docker_login")
    shell:
        """docker login --username=singlecellopenproblems --password=$(cat {input}) && \
        touch {output}"""

rule push_docker:
    input:
        build = "../docker/{image}/.docker_build",
        login = ".docker_login",
    output:
        "../docker/{image}/.docker_push"
    shell:
        "docker push singlecellopenproblems/{wildcards.image} && date +%s > {output}"

rule pull_docker:
    output:
        temp("../docker/{image}/.docker_pull")
    shell:
        "docker pull singlecellopenproblems/{wildcards.image} && touch {output}"
